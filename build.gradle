// Top-level build file where you can add configuration options common to all sub-projects/modules

buildscript {
    ext {
        // ----- SDK Compile and Build Versions ----- //

        // Minimum compileSDK version requirements
        // https://support.google.com/googleplay/android-developer/answer/11926878?hl=en
        compileSdkVersion = 34
        minSdkVersion = 21
        targetSdkVersion = 34


        // ----- App dependencies ----- //

        // AndroidX
        // https://developer.android.com/jetpack/androidx/explorer
        core_ktx_version = '1.12.0'
        activity_ktx_version = '1.8.0'
        fragment_ktx_version = '1.6.2'
        livedata_ktx_version = '2.4.1'
        appcompat_library_version = '1.6.1'
        annotations_version = '1.7.0'
        lifecycle_version = '2.6.2'
        constraint_layout_version = '2.1.4'
        exifinterface = '1.3.6'

        // Material Design
        // https://github.com/material-components/material-components-android/releases
        material_design_version = '1.10.0'

        // Google Play (For Google In-App Review)
        // https://developer.android.com/guide/playcore
        // https://developers.google.com/android/guides/setup
        play_services_base_version = '18.0.1'
        googlePlayReviewLibsVersions = "2.0.1"

        // Gson
        // https://github.com/google/gson
        gson_version = '2.10.1'


        // ----- Testing ----- //

        // AndroidX
        // https://developer.android.com/training/testing/set-up-project
        androidx_junit_ext_version = '1.1.5'
        androidx_test_version = '1.5.0'
        androidx_arch_core_version = '2.2.0'

        // Espresso
        // https://developer.android.com/training/testing/espresso
        espresso_core_version = '3.5.1'

        // Json
        // https://mvnrepository.com/artifact/org.json/json
        json_version = '20210307'

        // Truth
        // https://truth.dev/
        // https://github.com/google/truth
        truth_version = '1.1.3'

        // Mockk
        // https://mockk.io/
        mockk_version = '1.13.3'
    }

    project.ext {
        sonatypeVersion = '6.1.0'
        jfrogVersion = '6.1.0-private07'

        // Change this depending on where you are publishing to
        repoVersion = sonatypeVersion
    }

    repositories {
        mavenCentral()
        google()
    }

    allprojects {
        repositories {
            google()
            mavenCentral()
            maven {
                def artifactoryProperties = new Properties()
                if (project.rootProject.file('local.properties').exists()) {
                    artifactoryProperties.load(project.rootProject.file('local.properties').newDataInputStream())
                }
                url "https://apptentive.jfrog.io/artifactory/InternalNewAndroidSDK"
                credentials {
                    username = artifactoryProperties['artifactory_username'] ? artifactoryProperties['artifactory_username'] : ""
                    password = artifactoryProperties['artifactory_password'] ? artifactoryProperties['artifactory_password'] : ""
                }
            }
        }
    }

    dependencies {
        // Android Gradle
        // https://developer.android.com/studio/releases/gradle-plugin
        classpath "com.android.tools.build:gradle:8.1.2"

        // Kotlin Gradle
        // https://kotlinlang.org/docs/gradle.html
        // https://kotlinlang.org/docs/releases.html#release-details
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.20'

        // JFrog Gradle
        // https://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.24.20"
    }

}

plugins {
    // gradle-github-plugin
    // https://github.com/riiid/gradle-github-plugin
    id "co.riiid.gradle" version "0.4.2"

    // ktlint Gradle
    // https://github.com/JLLeitschuh/ktlint-gradle
    id "org.jlleitschuh.gradle.ktlint" version "10.2.1"

    // Gradle Nexus Publish Plugin
    // https://github.com/gradle-nexus/publish-plugin
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"

    // Gradle Dokka Plugin
    // https://github.com/Kotlin/dokka
    id "org.jetbrains.dokka" version "1.6.0"
    id 'org.jetbrains.kotlin.android' version '1.8.20' apply false
}

nexusPublishing {
    repositories {
        sonatype {
            Properties sonatypeProperties = new Properties()
            if (project.rootProject.file('local.properties').exists()) {
                sonatypeProperties.load(project.rootProject.file('local.properties').newDataInputStream())
            }
            username = sonatypeProperties.getProperty('sonatypeUsername') ? sonatypeProperties['sonatypeUsername'] : ""
            password = sonatypeProperties.getProperty('sonatypePassword') ? sonatypeProperties['sonatypePassword'] : ""
        }
    }
}

group = "com.apptentive"
version = sonatypeVersion

allprojects {
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'maven-publish'
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

repositories {
    google()
    mavenCentral()
}

apply from: "jfrog-publish-util.gradle"
apply from: "sonatype-publish-util.gradle"
